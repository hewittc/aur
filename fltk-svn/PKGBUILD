_pkgver=1.4
pkgver=r12940
pkgbase=fltk
pkgname=('fltk' 'fltk-docs' 'fltk-games')
pkgrel=1
arch=('x86_64')
license=('custom:LGPL')
url="http://www.fltk.org/"
makedepends=('mesa' 'glu' 'doxygen' 'libjpeg' 'libxft' 'libxinerama'
             'alsa-lib' 'desktop-file-utils' 'libxcursor')
source=("$pkgbase-svn"::"svn+http://anonymous:@seriss.com/public/$pkgbase/$pkgbase/branches/branch-$_pkgver" "fltk-pkg-config.patch")
sha256sums=('SKIP' '43fc331c21c4ed77c546db9bda441543c90d5e539e27ea5ac2de86898d907131')

pkgver() {
  cd "$pkgbase-svn"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "$pkgbase-svn"
  patch -p1 < "../../fltk-pkg-config.patch"
  sed -i 's/class Fl_XFont_On_Demand/class FL_EXPORT Fl_XFont_On_Demand/' FL/x.H
  sed -i 's/x-fluid.desktop/fluid.desktop/' -i fluid/Makefile
}

build() {
  cd "$pkgbase-svn"
  ./autogen.sh
  ./configure --prefix=/usr \
    --enable-threads \
    --enable-xft \
    --enable-shared
  make
  make -C documentation html
}

package_fltk() {
  pkgdesc="Graphical user interface toolkit for X"
  depends=('libjpeg' 'libxft' 'libxinerama' 'hicolor-icon-theme'
           'desktop-file-utils' 'xdg-utils' 'libxcursor' 'glu')
  options=('!docs')

  cd "$pkgbase-svn"
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C fluid install install-linux
  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_fltk-docs() {
  pkgdesc="Graphical user interface toolkit for X (documentation)"

  cd "$pkgbase-svn/documentation"
  install -t "$pkgdir/usr/share/doc/fltk/" -Dm644 html/*
  make DESTDIR="$pkgdir" -C ../test install
  install -Dm 644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_fltk-games() {
  pkgdesc="Example games that accompany FLTK: blocks, checkers and sudoku"
  depends=('alsa-lib' 'fltk')

  cd "$pkgbase-svn/test"
  make DESTDIR="$pkgdir" install-linux
  make DESTDIR="$pkgdir" -C ../documentation install-linux
  install -Dm 644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

