pkgname=libusb3380-git
pkgver=r6.5911b08
pkgrel=1
pkgdesc="USB3380 abstraction layer for libusb"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/xtrx-sdr/libusb3380"
license=('GPL2')

makedepends=('cmake')
depends=('libusb-compat')

provides=('libusb3380')
conflicts=('libusb3380')

source=(${pkgname}::"git+https://github.com/xtrx-sdr/libusb3380.git")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


build() {
  cd "$srcdir/$pkgname"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make 
}

package() {
  make -C "$srcdir/$pkgname/build" DESTDIR="$pkgdir" install
}
