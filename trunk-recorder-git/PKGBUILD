# Maintainer: Christopher Hewitt <hewittc@users.noreply.github.com>
pkgname=trunk-recorder-git
pkgver=3.0.1.r21.ga5453f2
pkgrel=1
pkgdesc="Records calls from a trunked radio system (P25 & SmartNet)"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/robotastic/trunk-recorder"
license=('GPL3')
depends=('boost' 'gnuradio' 'gnuradio-osmosdr' 'libuhd')
makedepends=('cmake')
conflicts=('trunk-recorder-git')
provides=('trunk-recorder')

_gitname=trunk-recorder
_gitbranch=master
source=(
  "$pkgname::git+https://github.com/robotastic/$_gitname#branch=$_gitbranch"
)
sha256sums=(
  'SKIP'
)

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  mkdir -p build
  cd build
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

package() {
  cd "$srcdir/$pkgname/build"
  make DESTDIR="$pkgdir" install
}

