# Maintainer: Christopher Hewitt <hewittc@users.noreply.github.com>
pkgname=hackrf-git
pkgver=2015.07.2.r78.gb278f05
pkgrel=1
pkgdesc="Driver for HackRF, allowing general purpose software defined radio (SDR)."
arch=('i686' 'x86_64')
url="https://github.com/mossmann/hackrf"
license=('GPL2')
depends=('libusb')
makedepends=('cmake')

_gitname=hackrf
_gitbranch=master
source=("hackrf::git+https://github.com/mossmann/$_gitname#branch=$_gitbranch"
        'hackrf.conf')
md5sums=('SKIP'
        'dbaf7f16be9f56d1007b5bb9a2979f64')
conflicts=('hackrf')
provides=('hackrf')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/^v//;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname/host"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$_gitname"
  pushd host/build
  make DESTDIR="$pkgdir" install
  popd
  pushd host/libhackrf
  install -vD -m644 53-hackrf.rules "$pkgdir/usr/lib/udev/rules.d/53-hackrf.rules"
  popd
  install -Dm644 "$srcdir/hackrf.conf" "$pkgdir/etc/modprobe.d/hackrf.conf"
}

