_pkgbase=epiphany
pkgname=epiphany-dkms
pkgver=r4.5f91f32
pkgrel=1
pkgdesc='Driver for Adapteva Parallella Epiphany eLink'
arch=('armv7h')
url='https://github.com/hewittc/epiphany'
license=('GPL2')

optdepends=('linux-headers' 'linux-lts-headers')
depends=('dkms')

_gitname=epiphany
_gitbranch=main
source=("${pkgname}::git+https://github.com/hewittc/$_gitname#branch=$_gitbranch"
        "dkms.conf.in")
sha256sums=('SKIP'
            '62426b047885b578c8735f1df23219eac4e98991e7f899d8ebb3c7b9631fce32')

pkgver() {
  cd ${pkgname}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  install -Dm644 ${srcdir}/dkms.conf.in ${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  sed -e "s/@PKGVER@/${pkgver}/" \
      -i "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

  mkdir -p ${pkgdir}/usr/src/${_pkgbase}-${pkgver}
  for f in {build_tools,drivers,Makefile}; do
    cp -Rp ${pkgname}/${f} ${pkgdir}/usr/src/${_pkgbase}-${pkgver}
  done
}

