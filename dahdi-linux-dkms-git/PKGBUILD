_pkgbase=dahdi
pkgname=dahdi-linux-dkms-git
pkgver=v3.1.0.r12.g5c840cf
pkgrel=1
pkgdesc='Drivers for telephony-related hardware'
arch=('i686' 'x86_64')
url='https://wiki.asterisk.org/wiki/display/DAHDI/DAHDI'
license=('GPL2')

optdepends=('linux-headers' 'linux-lts-headers')
depends=('libusb' 'perl' 'dkms')

conflicts=('dahdi-linux' 'zaptel')
provides=('dahdi-linux')

source=(${pkgname}::"git://git.asterisk.org/dahdi/linux.git#branch=next"
        "dahdi.service"
	"dahdi.rules"
        "dkms.conf"
        "000-forgotten-wctdm24xxp.patch")
sha256sums=('SKIP'
            '20057cf526abe95cdc34d58b843b17f35d0e591550e4088878f58321d91bd9cc'
            '9d941960433b8e6a4b5979ff3e63bcbabca39cd53ad98a536658d5db4eb76559'
            'e7923e1b571431bdade42afecbb178efbf94ebd482468bdf397f23976bd89157'
            'f7a74580121fedfe8baba044c6ab2983dbfd9c1b8c87b1936850d388c42af8c4')

pkgver() {
  cd ${pkgname}
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  patch -p1 < ../000-forgotten-wctdm24xxp.patch
}

package() {
  install -Dm644 ${srcdir}/dkms.conf ${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf
  install -Dm644 ${srcdir}/dahdi.service ${pkgdir}/usr/lib/systemd/system/dahdi.service
  install -Dm644 ${srcdir}/dahdi.rules ${pkgdir}/usr/lib/udev/rules.d/99-dahdi.rules

  sed -e "s/@_PKGBASE@/${_pkgbase}/" \
      -e "s/@PKGVER@/${pkgver}/" \
      -i "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

  mkdir -p ${pkgdir}/usr/src/${_pkgbase}-${pkgver}
  for f in {build_tools,drivers,include,Makefile}; do
  cp -Rp ${pkgname}/${f} ${pkgdir}/usr/src/${_pkgbase}-${pkgver}
  done

  mkdir -p ${pkgdir}/usr/include/dahdi
  cp -Rp ${pkgname}/include/dahdi/*.h ${pkgdir}/usr/include/dahdi/
}

