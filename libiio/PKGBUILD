# Maintainer: Christopher Hewitt <hewittc@users.noreply.github.com>
pkgname=libiio
pkgver=0.18
pkgrel=1
pkgdesc="Library for interfacing with IIO devices."
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/analogdevicesinc/libiio"
license=('GPL2')
depends=('libxml2' 'libusb' 'libaio')
makedepends=('cmake')
optmakedepends=('doxygen' 'graphviz' 'libserialport')

source=(
  "https://github.com/analogdevicesinc/libiio/archive/v$pkgver.tar.gz"
  "iiod.service"
  "000-doxyfile-path.patch"
)
sha256sums=(
  'bc2c5299974b65cfe9aa4a06d8c74d7651594e026bce416db48a2c5aa7ba2554'
  '8edb34d8b2209b019b18944a608c82490bd359f2506fbb65d4bb217b6763580f'
  '3657c6c4d3ed1c14d18be02b18c32ff7d29cfca3bc9c920c12882ca576da2832'
)
conflicts=('libiio')
provides=('libiio')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/"
  patch -p1 < ../000-doxyfile-path.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver/"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_SBINDIR=/usr/bin \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d ../ \
	-DCSHARP_BINDINGS=OFF
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/"
  cd build
  make DESTDIR="$pkgdir" install

  install -D -m644 ../../iiod.service "$pkgdir/usr/lib/systemd/system/iiod.service"
}

