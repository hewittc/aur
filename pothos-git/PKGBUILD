# Maintainer: Christopher Hewitt <hewittc@users.noreply.github.com>
pkgname=pothos-git
pkgver=pothos.0.5.1.r60.g69bbafcd
pkgrel=1
pkgdesc="The Pothos data-flow framework."
arch=('i686' 'x86_64')
url="https://github.com/pothosware/pothos"
license=('custom:Boost Software License Version 1.0')
depends=('apr' 'python' 'poco')
optdepends=('soapysdr' 'qwt')
makedepends=('cmake' 'doxygen')
conflicts=('pothos')
provides=('pothos')

_gitname=pothos
_gitbranch=master
source=("$pkgname::git+https://github.com/pothosware/$_gitname#branch=$_gitbranch" "https://raw.githubusercontent.com/nlohmann/json/v2.1.1/src/json.hpp")
sha256sums=('SKIP' 'faa2321beb1aa7416d035e7417fcfa59692ac3d8c202728f9bcc302e2d558f57')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/^v//;s/-/./g'
}

prepare() {
  cd "$srcdir/$pkgname"
  git submodule update --init --recursive

  mkdir -p "$srcdir/$pkgname/include/nlohmann"
  cp ../json.hpp "$srcdir/$pkgname/include/nlohmann"

  sed -i 's/find_package(Qwt)/#find_package(Qwt)/' plotters/CMakeLists.txt
}

build() {
  cd "$srcdir/$pkgname"
  mkdir -p build
  cd build
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DJSON_HPP_INCLUDE_DIR="$srcdir/$pkgname/include/nlohmann/" ../
  make
}

package() {
  cd "$srcdir/$pkgname/build"
  make DESTDIR="$pkgdir" install
}

