# Maintainer: Christopher Hewitt <hewittc@users.noreply.github.com>
pkgname=pothos-git
pkgver=pothos.0.5.0.r2.g2666b600
pkgrel=1
pkgdesc="The Pothos data-flow framework."
arch=('i686' 'x86_64')
url="https://github.com/pothosware/pothos"
license=('custom:Boost Software License Version 1.0')
depends=('apr' 'python' 'poco')
optdepends=('soapysdr' 'qwt')
makedepends=('cmake' 'doxygen')
conflicts=('pothos')
provides=('pothos')

_gitname=pothos
_gitbranch=master
source=("$pkgname::git+https://github.com/pothosware/$_gitname#branch=$_gitbranch")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/^v//;s/-/./g'
}

prepare() {
  cd "$srcdir/$pkgname"
  git submodule update --init --recursive

  sed -i 's/find_package(Qwt)/#find_package(Qwt)/' plotters/CMakeLists.txt
}

build() {
  cd "$srcdir/$pkgname"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$pkgname/build"
  make DESTDIR="$pkgdir" install
}

