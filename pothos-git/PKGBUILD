# Maintainer: Christopher Hewitt <hewittc@users.noreply.github.com>
pkgname=pothos-git
pkgver=r3076.534c3371
pkgrel=1
pkgdesc="The Pothos data-flow framework"
arch=('i686' 'x86_64')
url="https://github.com/pothosware/PothosCore"
license=('custom:boost')
depends=('python')
optdepends=('soapysdr' 'qwt' 'portaudio')
makedepends=('cmake' 'doxygen' 'nlohmann-json')
provides=('pothos')
conflicts=('pothos')

_gitname=pothos
_gitbranch=master
source=(
  "$pkgname::git+https://github.com/pothosware/$_gitname#branch=$_gitbranch"
)
sha256sums=(
  'SKIP'
)

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$pkgname"
  git submodule update --init --recursive
}

build() {
  mkdir -p "$srcdir/$pkgname/build"
  cd "$srcdir/$pkgname/build"
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_INTERNAL_POCO=ON \
        -DENABLE_INTERNAL_MUPARSERX=ON \
        -DENABLE_INTERNAL_SPUCE=ON \
        ..
  make
}

package() {
  cd "$srcdir/$pkgname/build"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/$pkgname/flow/Desktop/pothos-flow.desktop" "$pkgdir/usr/share/applications/pothos-flow.desktop"
  install -Dm644 "$srcdir/$pkgname/flow/Desktop/pothos-flow-128.png" "$pkgdir/usr/share/pixmaps/pothos-flow.png"
}
