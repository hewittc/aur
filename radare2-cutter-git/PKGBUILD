pkgname=radare2-cutter-git
pkgver=1.10.3.pre.r125.gb7d1059a
pkgrel=1
pkgdesc="A Qt and C++ GUI for radare2 reverse engineering framework"
arch=('i686' 'x86_64')
url="https://github.com/radareorg/cutter"
license=('GPL3' 'LGPL3')
makedepends=('cmake')
depends=('python' 'qt5-base' 'qt5-svg' 'qt5-webengine' 'icu' 'radare2' 'graphviz' 'shiboken2' 'pyside2')
provides=('radare2-cutter')

source=("${pkgname}::git+https://github.com/radareorg/cutter.git")
sha256sums=('SKIP')

pkgver() {
	cd ${pkgname}
	# Remove 'v' prefix on tags; prefix revision with 'r'; replace all '-' with '.'
	git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd ${pkgname}
	git submodule update --init --recursive

	cmake -s src -B build \
		-DCUTTER_USE_BUNDLED_RADARE2=ON \
		-DCUTTER_ENABLE_PYTHON=ON \
		-DCUTTER_ENABLE_PYTHON_BINDINGS=ON
}

build() {
	cd ${pkgname}
	cmake --build build
}

package() {
	cd ${pkgname}

	install -D -m755 build/Cutter "$pkgdir/usr/bin/Cutter"
	install -D -m644 src/org.radare.Cutter.desktop "${pkgdir}/usr/share/applications/org.radare.Cutter.desktop"
	install -D -m644 src/img/cutter.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/cutter.svg"
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -d -m755 docs/ "${pkgdir}/usr/share/doc/${pkgname}/"
	cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}/"
}

