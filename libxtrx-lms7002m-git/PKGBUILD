pkgname=libxtrx-lms7002m-git
pkgver=r243.c244dfb
pkgrel=1
pkgdesc="A portable driver for the LMS7002M dual transceiver in XTRX"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/xtrx-sdr/LMS7002M-driver/"
license=('APACHE')

makedepends=('cmake')

provides=('libxtrx-lms7002m')
conflicts=('libxtrx-lms7002m')

source=(${pkgname}::"git+https://github.com/xtrx-sdr/LMS7002M-driver.git")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


build() {
  cd "$srcdir/$pkgname"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=on -DLIB_LMS7002M_NAME="xtrx_lms7002m"
  make 
}

package() {
  make -C "$srcdir/$pkgname/build" DESTDIR="$pkgdir" install
}
